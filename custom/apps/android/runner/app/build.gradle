apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    if (gradle.ext.useLocalLibrary) {
        flatDir {
            dirs gradle.ext.localLibraryDir
        }
    }

    google()
}

android {
    compileSdkVersion 31
    flavorDimensions "default"
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId 'com.nativium.app'
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName '1.0.0'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias '[YOUR-KEY-ALIAS]'
            keyPassword '[YOUR-KEY-PASSWORD]'
            storeFile file('keystore.release.jks')
            storePassword '[YOUR-STORE-PASSWORD]'
        }
    }

    android {
        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            versionNameSuffix '-DEBUG'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    if (gradle.ext.useLocalLibrary) {
        if (gradle.ext.isDebuggable) {
            implementation(name: "library-debug", ext: "aar")
        } else {
            implementation(name: "library-release", ext: "aar")
        }
    } else {
        implementation project(':nativium-library')
    }

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'

    implementation 'com.google.android.material:material:1.5.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'com.google.android.instantapps:instantapps:1.1.0'

    // test only
    implementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

// copy webapp assets
task copyAssetsFiles(type: Copy)

copyAssetsFiles {
    description = 'Copy assets'
    from '../../../../../custom/modules/game/resources'
    into 'src/main/assets'
    include('**/*')
}

preBuild.dependsOn copyAssetsFiles
